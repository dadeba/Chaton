#!/usr/bin/env gosh
;; A prototype of API access gateway

(use www.cgi)
(use rfc.http)
(use file.util)
(use util.list)
(use srfi-1)
(use gauche.logger)

(define-constant +logfile+ "@@server-data-dir@@/logs/api.log")
(define-constant +poster+  "@@httpd-url@@@@url-path@@@@cgi-script@@")
(define-constant +viewer+  "@@httpd-url@@:@@comet-port@@/")
(define-constant +icon+    "@@icon-url@@")
(define-constant +credential+ "@@server-data-dir@@auth")

(define (main args)
  (set! (port-buffering (current-error-port)) :line)
  (log-open +logfile+)
  (cgi-main handle-request))

(define (handle-request ps)
  (let1 m (cgi-get-metavariable "REQUEST_METHOD")
    (if (not (equal? m "POST"))
      (reply-error "405 Method not allowed" m)
      (check-login ps))))

(define (check-login ps)
  (let1 who (cgi-get-parameter "who" ps)
    (cond [(not who) (reply-error "401 Unauthorized" "'who' parameter missing")]
          [(get-cid) => (cut reply-ok who <>)]
          [(reply-error "503 Service Unavailable" "couldn't obtain cid")])))

(define (get-cid)
  (and-let* ([ (file-exists? +credential+) ]
             [cred (file->string +credential+)])
    (rxmatch-let (#/\/\/([^\/]+)\// +viewer+) (_ host)
      (receive (status hdrs body) (http-get host #`"/getcid?cred=,cred")
        (and (equal? status "200")
             (guard (e [(<read-error> e) #f])
               (let1 p (read-from-string body)
                 (and (list? p) (every pair? p) p))))))))

(define (reply-ok name params)
  (log-format "authenticated ~a (~a)" name (remote-addr))
  `(,(cgi-header :content-type "application/x-sexpr; charset=utf-8"
                 :cache-control "no-cache")
    ,(write-to-string
      `((post-uri . ,+poster+) (comet-uri . ,+viewer+)
        (icon-uri . ,+icon+) (room-name . "@@room-name@@") ,@params))))

(define (reply-error msg reason)
  (log-format "~a: ~a (~a)" msg reason (remote-addr))
  (cgi-header :status msg))

(define (remote-addr) (cgi-get-metavariable "REMOTE_ADDR"))

;; Local variables:
;; mode: scheme
;; end:
